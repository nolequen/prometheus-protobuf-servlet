group 'su.nlq'
version '0.2.0'

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'jacoco'

sourceCompatibility = 1.8

repositories {
  mavenCentral()
}

dependencies {
  compile group: 'org.jetbrains', name: 'annotations', version: '16.0.1'
  compile group: 'com.google.protobuf', name: 'protobuf-java', version: '3.5.1'
  compile group: 'io.prometheus', name: 'simpleclient', version: '0.3.0'

  compileOnly group: 'javax.servlet', name: 'javax.servlet-api', version: '4.0.0'

  testCompile group: 'junit', name: 'junit', version: '4.12'
  testCompile group: 'org.eclipse.jetty', name: 'jetty-servlet', version: '9.4.9.v20180320'
  testCompile group: 'org.eclipse.jetty', name: 'jetty-client', version: '9.4.9.v20180320'
}

jacocoTestReport {
  reports {
    xml.enabled true
    html.enabled false
  }
  afterEvaluate {
    classDirectories = files(classDirectories.files.collect {
      fileTree(dir: it, exclude: ['**/*Metrics**'])
    })
  }
}

check.dependsOn jacocoTestReport

test.finalizedBy(project.tasks.jacocoTestReport)


uploadArchives {
  repositories {
    mavenDeployer {
      beforeDeployment { final MavenDeployment deployment -> signing.signPom(deployment) }

      def user = project.hasProperty("ossrhUsername") ? ossrhUsername : ''
      def password = project.hasProperty("ossrhUsername") ? ossrhPassword : ''

      repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
        authentication(userName: user, password: password)
      }

      snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
        authentication(userName: user, password: password)
      }

      pom.project {
        name 'Prometheus Protobuf Servlet'
        description 'Protobuf formatter and HTTP servlet for Prometheus client'
        url 'https://github.com/nolequen/prometheus-protobuf-servlet'
        licenses {
          license {
            name "The Apache Software License, Version 2.0"
            url "http://www.apache.org/licenses/LICENSE-2.0.txt"
            distribution 'repo'
          }
        }
        scm {
          url 'https://github.com/nolequen/prometheus-protobuf-servlet'
          connection 'scm:git:git://github.com/nolequen/prometheus-protobuf-servlet.git'
          developerConnection 'scm:git:ssh://github.com:nolequen/prometheus-protobuf-servlet.git'
        }
        developers {
          developer {
            name "Nolequen"
            email "nolequen@gmail.com"
            url "http://www.nlq.su/"
          }
        }
      }
    }
  }
}

task sourcesJar(type: Jar, dependsOn: classes) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}

artifacts {
  archives sourcesJar, javadocJar
}

signing {
  sign configurations.archives
}